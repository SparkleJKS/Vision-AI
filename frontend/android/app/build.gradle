apply plugin: "com.android.application"
apply plugin: "org.jetbrains.kotlin.android"
// react-native-config: point to .env in project root (where package.json lives)
project.ext.defaultEnvFile = "${rootProject.projectDir.parent}/.env"
apply from: "${rootProject.projectDir.parent}/node_modules/react-native-config/android/dotenv.gradle"
apply plugin: "com.facebook.react"
apply plugin: "com.google.gms.google-services"

def projectRoot = rootDir.getAbsoluteFile().getParentFile().getAbsolutePath()
if (!rootProject.hasProperty("VisionCamera_enableCodeScanner") || !rootProject.property("VisionCamera_enableCodeScanner").toString().toBoolean()) {
    rootProject.ext.set("VisionCamera_enableCodeScanner", true)
}

// Patched React Native header for NDK 26 (std::format not in libc++) - copied so app/autolinked native build use it
def patchedReactHeadersDir = file("${buildDir}/patched_react_headers")
def sourceGraphicsConversions = file(["node", "--print", "require.resolve('react-native/package.json')"].execute(null, rootDir).text.trim()).getParentFile().toPath().resolve("ReactCommon/react/renderer/core/graphicsConversions.h").toFile()
tasks.register("copyPatchedReactHeaders", Copy) {
  from sourceGraphicsConversions
  into file("${patchedReactHeadersDir}/react/renderer/core")
  onlyIf { sourceGraphicsConversions.exists() }
}

// Single source of truth
def frontendRoot = rootProject.projectDir.parent
def tfliteSource = file("${frontendRoot}/assets/yolov8n.tflite")
def tfliteDestDir = file("${projectDir}/src/main/assets")
tasks.register("copyTfliteModel", Copy) {
  from tfliteSource
  into tfliteDestDir
  onlyIf { tfliteSource.exists() }
}

/**
 * This is the configuration block to customize your React Native Android app.
 * By default you don't need to apply any configuration, just uncomment the lines you need.
 */
react {
    entryFile = file("$projectRoot/index.js")
    reactNativeDir = new File(["node", "--print", "require.resolve('react-native/package.json')"].execute(null, rootDir).text.trim()).getParentFile().getAbsoluteFile()
    hermesCommand = new File(["node", "--print", "require.resolve('react-native/package.json')"].execute(null, rootDir).text.trim()).getParentFile().getAbsolutePath() + "/sdks/hermesc/%OS-BIN%/hermesc"
    codegenDir = new File(["node", "--print", "require.resolve('@react-native/codegen/package.json', { paths: [require.resolve('react-native/package.json')] })"].execute(null, rootDir).text.trim()).getParentFile().getAbsoluteFile()

    enableBundleCompression = (findProperty('android.enableBundleCompression') ?: true).toBoolean()

    /* Folders */
     //   The root of your project, i.e. where "package.json" lives. Default is '../..'
    // root = file("../../")
    //   The folder where the react-native NPM package is. Default is ../../node_modules/react-native
    // reactNativeDir = file("../../node_modules/react-native")
    //   The folder where the react-native Codegen package is. Default is ../../node_modules/@react-native/codegen
    // codegenDir = file("../../node_modules/@react-native/codegen")

    /* Variants */
    //   The list of variants to that are debuggable. For those we're going to
    //   skip the bundling of the JS bundle and the assets. By default is just 'debug'.
    //   If you add flavors like lite, prod, etc. you'll have to list your debuggableVariants.
    debuggableVariants = ["devDebug"]

    /* Bundling */
    //   A list containing the node command and its flags. Default is just 'node'.
    // nodeExecutableAndArgs = ["node"]

    //
    //   The path to the CLI configuration file. Default is empty.
    // bundleConfig = file(../rn-cli.config.js)
    //
    //   The name of the generated asset file containing your JS bundle
    // bundleAssetName = "MyApplication.android.bundle"
    //
    //   The entry file for bundle generation. Default is 'index.android.js' or 'index.js'
    // entryFile = file("../js/MyApplication.android.js")
    //
    //   A list of extra flags to pass to the 'bundle' commands.
    //   See https://github.com/react-native-community/cli/blob/main/docs/commands.md#bundle
    // extraPackagerArgs = []

    /* Hermes Commands */
    //   The hermes compiler command to run. By default it is 'hermesc'
    // hermesCommand = "$rootDir/my-custom-hermesc/bin/hermesc"
    //
    //   The list of flags to pass to the Hermes compiler. By default is "-O", "-output-source-map"
    // hermesFlags = ["-O", "-output-source-map"]

    /* Autolinking */
    autolinkLibrariesWithApp()
}

/**
 * Set this to true in release builds to optimize the app using [R8](https://developer.android.com/topic/performance/app-optimization/enable-app-optimization).
 */
def enableMinifyInReleaseBuilds = (findProperty('android.enableMinifyInReleaseBuilds') ?: false).toBoolean()

/**
 * The preferred build flavor of JavaScriptCore (JSC)
 *
 * For example, to use the international variant, you can use:
 * `def jscFlavor = 'org.webkit:android-jsc-intl:+'`
 *
 * The international variant includes ICU i18n library and necessary data
 * allowing to use e.g. `Date.toLocaleString` and `String.localeCompare` that
 * give correct results when using with locales other than en-US. Note that
 * this variant is about 6MiB larger per architecture than default.
 */
def jscFlavor = 'io.github.react-native-community:jsc-android:2026004.+'

android {
    // Pin NDK 26 so app/codegen link uses same C++ stdlib (avoids undefined symbol errors)
    ndkVersion "26.1.10909125"

    buildToolsVersion rootProject.ext.buildToolsVersion
    compileSdk rootProject.ext.compileSdkVersion

    namespace 'com.anonymous.VisionAI'
    flavorDimensions "environment"
    productFlavors {
        dev {
            applicationIdSuffix ".dev"
            dimension "environment"
        }
    }
    defaultConfig {
        applicationId 'com.anonymous.VisionAI'
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName "1.0.0"

        buildConfigField "String", "REACT_NATIVE_RELEASE_LEVEL", "\"${findProperty('reactNativeReleaseLevel') ?: 'stable'}\""
        // react-native-config: BuildConfig lives in namespace package, not applicationId
        resValue "string", "build_config_package", "com.anonymous.VisionAI"
    }
    // Add patched header dir in same phase as React plugin (finalizeDsl) so CMake receives it
    project.pluginManager.withPlugin("com.android.application") {
        project.extensions.getByType(com.android.build.api.variant.ApplicationAndroidComponentsExtension).finalizeDsl { ext ->
            def patchedDir = file("${layout.buildDirectory.get().asFile}/patched_react_headers").absolutePath.replace('\\', '/')
            if (ext?.externalNativeBuild?.cmake != null && ext.defaultConfig?.externalNativeBuild?.cmake?.arguments != null) {
                def args = ext.defaultConfig.externalNativeBuild.cmake.arguments
                if (!args.any { it?.startsWith("-DREACT_PATCHED_HEADERS_DIR=") }) {
                    args.add("-DREACT_PATCHED_HEADERS_DIR=${patchedDir}")
                    println "VisionAI: Added REACT_PATCHED_HEADERS_DIR=${patchedDir}"
                }
            }
        }
    }
    afterEvaluate {
        tasks.matching { it.name.startsWith("configureCMake") }.configureEach { it.dependsOn("copyPatchedReactHeaders") }
        tasks.matching { it.name.startsWith("merge") && it.name.endsWith("Assets") }.configureEach { it.dependsOn("copyTfliteModel") }
        // Lint tasks read src/main/assets; must run after copyTfliteModel (Gradle implicit-dependency validation)
        tasks.matching { it.name.toLowerCase().contains("lintvital") }.configureEach { it.dependsOn("copyTfliteModel") }
    }
    signingConfigs {
        debug {
            storeFile file('debug.keystore')
            storePassword 'android'
            keyAlias 'androiddebugkey'
            keyPassword 'android'
        }
    }
    buildTypes {
        debug {
            signingConfig signingConfigs.debug
        }
        release {
            // Caution! In production, you need to generate your own keystore file.
            // see https://reactnative.dev/docs/signed-apk-android.
            signingConfig signingConfigs.debug
            def enableShrinkResources = findProperty('android.enableShrinkResourcesInReleaseBuilds') ?: 'false'
            shrinkResources enableShrinkResources.toBoolean()
            minifyEnabled enableMinifyInReleaseBuilds
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
            def enablePngCrunchInRelease = findProperty('android.enablePngCrunchInReleaseBuilds') ?: 'true'
            crunchPngs enablePngCrunchInRelease.toBoolean()
        }
    }
    packagingOptions {
        jniLibs {
            useLegacyPackaging = false
        }
    }
    androidResources {
        ignoreAssetsPattern '!.svn:!.git:!.ds_store:!*.scc:!CVS:!thumbs.db:!picasa.ini:!*~'
    }
    sourceSets {
        main {
            assets.srcDirs = ['src/main/assets']
        }
    }
}

// Apply static values from `gradle.properties` to the `android.packagingOptions`
// Accepts values in comma delimited lists, example:
// android.packagingOptions.pickFirsts=/LICENSE,**/picasa.ini
["pickFirsts", "excludes", "merges", "doNotStrip"].each { prop ->
    // Split option: 'foo,bar' -> ['foo', 'bar']
    def options = (findProperty("android.packagingOptions.$prop") ?: "").split(",");
    // Trim all elements in place.
    for (i in 0..<options.size()) options[i] = options[i].trim();
    // `[] - ""` is essentially `[""].filter(Boolean)` removing all empty strings.
    options -= ""

    if (options.length > 0) {
        println "android.packagingOptions.$prop += $options ($options.length)"
        // Ex: android.packagingOptions.pickFirsts += '**/SCCS/**'
        options.each {
            android.packagingOptions[prop] += it
        }
    }
}

dependencies {
    // The version of react-native is set by the React Native Gradle Plugin
    implementation("com.facebook.react:react-android")

    // TensorFlow Lite CPU runtime
    implementation("org.tensorflow:tensorflow-lite:2.14.0")
    implementation("org.tensorflow:tensorflow-lite-gpu:2.14.0")
    implementation("org.tensorflow:tensorflow-lite-gpu-api:2.14.0")

    // GIF and WebP support
    implementation("com.facebook.fresco:animated-gif:3.4.0")
    implementation("com.facebook.fresco:webpsupport:3.4.0")

    if (hermesEnabled.toBoolean()) {
        implementation("com.facebook.react:hermes-android")
    } else {
        implementation jscFlavor
    }
}

apply plugin: 'com.google.firebase.crashlytics'

